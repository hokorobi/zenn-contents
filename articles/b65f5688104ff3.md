---
title: "Vim 本体の自動補完設定試用"
emoji: "🦁"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["Vim"]
published: false
---

:::message
この記事は [Vim駅伝](https://vim-jp.org/ekiden/) 2025-09-29 の記事です。
:::

Vim 本体の自動補完が充実してきたので [VimComplete が Deprecate になりました。](https://github.com/girishji/vimcomplete/commit/e60622ed084892321b96721edaa0a76c973afce8)
本体の自動補完に移行すべく以下のような設定で試しています。
ポップアップの補完候補を選択しても挿入はされないみたいなので `<C-l>` で挿入。

## 使用プラグイン

それぞれのインストール方法などは書きません。使える状態になっているものとして以下記述します。

- [yegappan/lsp](https://github.com/yegappan/lsp)
- [kana/vim-submode](https://github.com/kana/vim-submode) : 使わない設定に変えたらなくてもいい。
- [hrsh7th/vim-vsnip](https://github.com/hrsh7th/vim-vsnip) : snippet
- [hrsh7th/vim-vsnip-integ](https://github.com/hrsh7th/vim-vsnip-integ) : vsnip を lsp で使えるように。
- [rafamadriz/friendly-snippets](https://github.com/rafamadriz/friendly-snippets) : vsnip で使う snippet 集。自分に合うかはわからない。とりあえずインストールしただけ。

## 使用lsp

- [Deno](https://deno.com)
- [Gopls](https://go.dev/gopls/)
- [Taplo](https://taplo.tamasfe.dev/)

## 自動補完の設定

- 自動補完の開始は 0.2 秒後に
- `complete` の変更
  - t 削除: 自分の環境ではタグ補完で固まるようだったので
  - b^5: 「バッファリスト内の、現在読み込まれている別のバッファから検索」ということなので関係ないものがありそうだからマッチ数を減らしてみる。ヘルプを開いていると日本語がわんさか出てくるみたい。
  - k 追加: plantuml, autohotkey のファイルタイプで dictionary を指定しているので、そこから補完。

```Vim:vimrc
set autocomplete
set autocompletedelay=200
set complete=.,k,w,b^5,u,i
set completeopt-=preview completeopt+=menuone completeopt+=fuzzy
```

## filetype ごとの設定

### autohotkey

https://github.com/fincs/SciTE4AutoHotkey/ の source/ahk2.standard.api と source/ahk.keys.api を結合したものを dictionary へ。
https://github.com/AutoHotkey/AutoHotkeyDocs/tree/v2 をダウンロードして syntaxgen\CreateAhk2.ahk, syntaxgen\CreateKeys.ahk で再生成してみたけど変わっていなかった。

```Vim:~/_vim/after/ftplugin/autohotkey.vim
setlocal dictionary=~/path/to/autohotkey.txt
```

### go

go の補完は yegappan/lsp の自動補完を使うので本体の補完が出ないように complete を空にしてみた。
9.1.1779 で autocmoplete がローカルで設定できるようになるので、そうなったら変更する。
[patch 9.1.1779: completion: 'autocomplete' cannot be enabled per buffer · vim/vim@0208b3e](https://github.com/vim/vim/commit/0208b3e80a080740ff77a5661d0f65090e317d90)

```Vim:~/_vim/after/ftplugin/go.vim
setlocal complete=
```

### plantuml

plantuml.jar -language で出力した内容を dictionary へ

```Vim:~/_vim/after/ftplugin/plantuml.vim
setlocal dictionary=~/path/to/plantuml.txt
```

### typescript

go と同様

```Vim:~/_vim/after/ftplugin/typescript.vim
setlocal complete=
```

## yegappan/lsp の設定

```Vim:lsp.vim
function s:lspSet() abort
  let lspOpts = #{
        \ autoComplete: v:false,
        \ condensedCompletionMenu: v:true,
        \}
  call LspOptionsSet(lspOpts)

  let lspServers = [
        \ #{name: 'deno',
        \   filetype: ['typescript'],
        \   path: 'deno',
        \   args: ['lsp'],
        \ },
        \ #{name: 'gopls',
        \   filetype: 'go',
        \   path: fnamemodify('~/bin/gopls.exe', ':p')->substitute('\', '/', 'g'),
        \   args: ['serve'],
        \   syncInit: v:true,
        \ },
        \ #{name: 'taplo',
        \   filetype: 'toml',
        \   path: 'taplo',
        \   args: ['lsp', 'stdio'],
        \ },
        \]
  call LspAddServer(lspServers)
endfunction
autocmd vimrc User LspSetup call s:lspSet()

function s:LspHas(feature)
  return !lsp#buffer#CurbufGetServer(a:feature)->empty()
endfunction

function s:lspKeymap() abort
  if s:LspHas("hover")
    setl keywordprg=:LspHover
  endif
  if s:LspHas("declaration")
    nnoremap <buffer> gd :LspGotoDeclaration<CR>
    nnoremap <buffer> gD :LspPeekDeclaration<CR>
  endif
  nnoremap <buffer> <Space>al <Cmd>LspDiagShow<CR>
  nnoremap <buffer> <C-]> <Cmd>LspGotoDefinition<CR>
  nnoremap <buffer> <Space>ar <Cmd>LspRename<CR>
  nnoremap <buffer> <Space>aR <Cmd>LspReferences<CR>

  " submode
  call submode#enter_with('lsp', 'n', 'br', '<Space>aj', '<Cmd>LspDiagNext<CR>')
  call submode#enter_with('lsp', 'n', 'br', '<Space>an', '<Cmd>LspDiagNext<CR>')
  call submode#enter_with('lsp', 'n', 'br', '<Space>ak', '<Cmd>LspDiagPrev<CR>')
  call submode#enter_with('lsp', 'n', 'br', '<Space>ap', '<Cmd>LspDiagPrev<CR>')
  call submode#map('lsp', 'n', 'brs', 'j', '<Cmd>LspDiagNext<CR>')
  call submode#map('lsp', 'n', 'brs', 'n', '<Cmd>LspDiagNext<CR>')
  call submode#map('lsp', 'n', 'brs', 'k', '<Cmd>LspDiagPrev<CR>')
  call submode#map('lsp', 'n', 'brs', 'p', '<Cmd>LspDiagPrev<CR>')
endfunction
autocmd vimrc User LspAttached call s:lspKeymap()
```

## vsnip の設定

```Vim:vsnip.vim
imap <silent><expr> <Tab>
      \ pumvisible() ? '<Down>' :
      \ vsnip#jumpable(1)  ? '<Plug>(vsnip-jump-next)' :
      \ '<Tab>'
smap <silent><expr> <Tab>  vsnip#jumpable(1)  ? '<Plug>(vsnip-jump-next)' : '<Tab>'
imap <silent><expr> <S-Tab>
      \ pumvisible() ? '<Up>' :
      \ vsnip#jumpable(-1)  ? '<Plug>(vsnip-jump-prev)' :
      \ '<C-h>'
smap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : '<S-Tab>'
imap <silent><expr> <C-j>
      \ pumvisible() ? '<C-n>' :
      \ '<C-j>'
imap <silent> <C-n> <Down>
imap <silent><expr> <C-k>
      \ pumvisible() ? '<C-p>' :
      \ '<Cmd>normal! D<CR>'
imap <silent> <C-p> <Up>
imap <silent><expr> <C-l>
      \ pumvisible() ? '<C-y>' :
      \ vsnip#expandable() ? '<Plug>(vsnip-expand)' :
      \ vsnip#jumpable(1) ? '<Plug>(vsnip-jump-next)' :
      \ '<C-l>'

" 各環境に従って変更
let g:vsnip_snippet_dirs = [
      \ expand('~/vimfiles/plug/vsnip'),
      \ expand('~/_vim/path/to/friendly-snippets/snippets'),
      \]
```
